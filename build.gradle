//noinspection GroovyAssignabilityCheck
allprojects {

    apply from: "$rootProject.rootDir/scala.gradle"

    ext {
        scalaVersion = '2.13.3'
    }

    apply plugin: 'scala'
    apply plugin: 'java-library'


    group 'io.github.iltotore'
    version '1.2.1'

    repositories {
        mavenCentral()
        jcenter()
    }

    afterEvaluate {
        dependencies {
            api scalaLib()
            api scalaDependency('com.lightbend.akka', 'akka-stream-alpakka-file', '2.0.1')
            api scalaDependency('com.typesafe.akka', 'akka-http', '10.2.0-M1')
            api scalaDependency('com.typesafe.akka', 'akka-stream', '2.6.5')
            api scalaDependency('com.typesafe.akka', 'akka-http-spray-json', '10.2.0-M1')
            api scalaDependency('com.desmondyeung.hashing', 'scala-hashing', '0.1.0')
            api scalaDependency('io.spray', 'spray-json', '1.3.5')
            api 'commons-io:commons-io:2.6'
        }
    }

    compileScala.options.encoding = compileJava.options.encoding = compileTestScala.options.encoding = compileTestJava.options.encoding = 'UTF-8'
}

task cleanRelease(type: Delete) {
    delete file("$buildDir/release")
}

afterEvaluate {
    task release(type: Copy) {
        dependsOn cleanRelease
        dependsOn ':server:clean'
        dependsOn ':server:shadowJar'
        dependsOn ':client:clean'
        dependsOn ':client:shadowJar'
        dependsOn ':client:sourcesJar'
        dependsOn ':client:javadocJar'
        dependsOn ':client:scalaJar'

        rename('(.*)-javadoc.jar', '$1-scaladoc.jar')

        from "${project(':client').buildDir}/libs/", "${project(':server').buildDir}/libs"
        into "$buildDir/release"

        group 'build'
    }
}