allprojects {
    ext {
        scalaVersion = '2.13.1'
    }
    apply plugin: 'scala'

    group 'io.github.iltotore'
    version '1.0'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        implementation scalaLib()
        implementation scalaDependency('com.typesafe.akka', 'akka-http', '10.2.0-M1')
        implementation scalaDependency('com.typesafe.akka', 'akka-stream', '2.6.5')
        implementation scalaDependency('com.typesafe.akka', 'akka-http-spray-json', '10.2.0-M1')
        implementation scalaDependency('com.desmondyeung.hashing', 'scala-hashing', '0.1.0')
        implementation scalaDependency('io.spray', 'spray-json', '1.3.5')
        implementation 'commons-io:commons-io:2.6'
    }

    compileScala.options.encoding = compileJava.options.encoding = compileTestScala.options.encoding = compileTestJava.options.encoding = 'UTF-8'
}

def getMajorScalaVersion() {
    String[] parts = scalaVersion.tokenize('.')
    return parts[0] + '.' + parts[1]
}

def scalaModule(String name, String version) {
    return scalaDependency('org.scala-lang.modules', name, version)
}

def scalaDependency(String company, String name, String version) {
    return company + ':' + name + '_' + getMajorScalaVersion() + ':' + version
}

def scalaLib() {
    return 'org.scala-lang:scala-library:' + scalaVersion
}